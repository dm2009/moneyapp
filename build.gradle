group "gradlemini"
version "1.0-SNAPSHOT"

apply plugin: "java"
apply plugin: "war"
apply plugin: "com.bmuschko.tomcat"
apply plugin: "checkstyle"
apply plugin: "findbugs"
apply plugin: "pmd"

repositories {
    mavenCentral()
    jcenter()
}

ext {
    tomcatVersion = "7.0.69"

    javaxServlet = "3.1.0"
    javaxServletJsp = "2.1"
    springVersion = "4.2.5.RELEASE"
    commonsConfigurationVersion = "1.10"
    commonsDbcpVersion = "1.3"
    slf4jVersion = "1.7.21"
    postgresqlVersion = "9.3-1100-jdbc41"
    validationVersion = "1.1.0.Final"
    jstlVersion = "1.2"
}

dependencies {
    providedCompile "javax.servlet:javax.servlet-api:$javaxServlet"
    providedCompile "javax.servlet.jsp:jsp-api:$javaxServletJsp"
    compile group: "org.slf4j", name: "slf4j-log4j12", version: "$slf4jVersion"

    compile group: "commons-configuration", name: "commons-configuration", version: "$commonsConfigurationVersion"

    compile group: "commons-dbcp", name: "commons-dbcp", version: "$commonsDbcpVersion"

    compile group: "org.postgresql", name: "postgresql", version: "$postgresqlVersion"

    compile group: "org.springframework", name: "spring-core", version: "$springVersion"
    compile group: "org.springframework", name: "spring-webmvc", version: "$springVersion"
    compile group: "org.springframework", name: "spring-jdbc", version: "$springVersion"

    compile group: "javax.validation", name: "validation-api", version: "$validationVersion"

    runtime("javax.servlet:jstl:$jstlVersion")

}

dependencies {

    tomcat "org.apache.tomcat.embed:tomcat-embed-core:$tomcatVersion",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:$tomcatVersion",
            "org.apache.tomcat.embed:tomcat-embed-jasper:$tomcatVersion"
}

buildscript {

    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.bmuschko:gradle-tomcat-plugin:2.2.5"
    }
}

checkstyle  {

    configFile = rootProject.file("tools/checkstyle/sun_checks.xml")
    ignoreFailures = true
    showViolations = false
}

checkstyleTest {
    enabled = false
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
        html.destination rootProject.file("build/reports/checkstyle/checkstyle_report.html")
    }
}


task customFindbugs(type: FindBugs){
            source = fileTree('src/main/java/')
            classpath = files()
            ignoreFailures = true
            effort = "max"
            reportLevel = "high"
        }


findbugs {
    sourceSets = [sourceSets.main]
    ignoreFailures = true
    effort = "max"
    reportLevel = "high"
    //visitors = ["FindSqlInjection", "SwitchFallthrough"]
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
        html.destination rootProject.file("build/reports/findbugs/findbugs_report.html")
    }
}

pmd {
    ignoreFailures = true
    consoleOutput = false
    ruleSets = ["java-basic", "java-strings", "java-braces"]
    reportsDir = file("${rootProject.rootDir}/build/reports/pmd")

}

// add CPD to check
check << {
    File outDir = new File('build/reports/pmdcpd/')
    outDir.mkdirs()
    ant.taskdef(name: 'cpd', classname: 'net.sourceforge.pmd.cpd.CPDTask',
            classpath: configurations.pmd.asPath)

    ant.cpd(minimumTokenCount: "100", format: 'xml',
            outputFile: new File(outDir , 'cpd.xml')) {
        fileset(dir: "src/main/java") {
            include(name: '**/*.java')
        }
    }
    //<xslt in="${report.pmd.dir}/cpd_report.xml" style="${pmd.home.dir}/etc/xslt/cpdhtml.xslt" out="${report.pmd.dir}/cpd_report.html" />
    ant.xslt(in: file("${outDir}/cpd.xml"), style: rootProject.file("tools/pmd/cpdhtml.xslt"), out: "${outDir}/cpd_report.html")
}


task copyTask (type: Copy ){
    from 'html'
    into 'build/reports'
    include '**/*.htm'
}

check.dependsOn copyTask





